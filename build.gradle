plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.5-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.archives_base_name
    }

    version = "${rootProject.mod_version}-${project.name}"
    group = rootProject.maven_group

    repositories {
        maven { // Flywheel
            url = "https://maven.tterrag.com/"
            content {
                includeGroup("com.jozufozu.flywheel")
            }
        }
        maven { // DragonLib
            name = "MrJulsen's Mod Resources"
            url = "https://raw.githubusercontent.com/MisterJulsen/modsrepo/main/maven"
        }
        maven { // Forge Config Api (required for fabric version of DragonLib)
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }

          
        maven { // Create Forge and Registrate Forge
            url = "https://maven.tterrag.com/"
            content {
                includeGroup("com.tterrag.registrate")
                includeGroup("com.simibubi.create")
            }
        }

        maven { url = "https://maven.shedaniel.me/" } // Cloth Config, REI
        maven { url = "https://maven.blamejared.com/" } // JEI
        maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
        maven { url = "https://maven.quiltmc.org/repository/release" } // Quilt Mappings
        maven { url = "https://api.modrinth.com/maven" } // LazyDFU
        maven { url = "https://maven.terraformersmc.com/releases/" } // Mod Menu
        maven { url = "https://mvn.devos.one/snapshots/" } // Create, Porting Lib, Forge Tags, Milk Lib, Registrate
        maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // Forge Config API Port
        maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
        maven { url = "https://jitpack.io/" } // Mixin Extras, Fabric ASM
        maven { url = "https://maven.tterrag.com/" } // Flywheel
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

publishing {
    repositories {
        maven {
            name "GitHub"
            url "file://${System.getProperty('user.home')}/.m2/github/modsrepo/maven"
        }
    }
}

    java {
        withSourcesJar()
        withJavadocJar()
    }
}
